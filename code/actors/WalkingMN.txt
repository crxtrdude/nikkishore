// ZKVN Engine
// Decorate

// Mr Nobody
ACTOR WalkingMN : BasicPede 1338
{
	//$Category ZK-VN Demo
	//$Title "Mr. Nobody"
	//$Sprite TROOA1
	Speed 8
	Radius 20
	Height 56
	Activation THINGSPEC_Switch|THINGSPEC_ThingTargets
	+USESPECIAL
	States
	{
	Spawn:
		TROO A 1 A_Wander
		Loop
	Active:
		//Only if pede is attacked, you skip activation
		TNT1 A 0 A_JumpIfInventory ("PedeAttackedCheck",1,"PedeProvoked")
	
		TNT1 A 0 A_ChangeFlag("USESPECIAL", false)
		
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ACS_NamedExecuteWithResult("SetHoldTime", -1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_MRNOBODY, 1, 1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoiceStart") // Using AddChoiceStart with no parameters will make it use type 0 without numbering
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoice", 1, 1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoice", 2, 2)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoiceEnd")
		TROO A 1 A_JumpIf(CallACS("ChoiceDone"), 1)
		Wait
		TNT1 A 0 A_JumpIf(1 == CallACS("ChoiceMade"), "Choice1")
		TNT1 A 0 A_JumpIf(2 == CallACS("ChoiceMade"), "Choice2")
		Goto Done
		
	Choice1:
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_MRNOBODY, 1, 2)
		Goto Done
	Choice2:
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_MRNOBODY, 1, 3)
		Goto Done
		
	Done:
		// Here we wait for the stack to empty (all dialogue to be cleared)
		TROO A 1 A_JumpIf(CallACS("IsQueueEmpty"), 1)
		Wait
		TNT1 A 0 A_ChangeFlag("USESPECIAL", true)
		Goto Spawn
	Pain:
    QUUU A 3
    QUUU A 3 A_Pain
    TNT1 A 0 A_ChangeFlag("USESPECIAL", false)
    TNT1 A 0 A_JumpIfInventory("PedeAttackedCheck", 1, "Melee") 
    TNT1 A 0 A_GiveInventory("PedeAttackedCheck", 1)
  	Goto Melee
	}
}