//===========================================================================
//
// CarPlayer
//
//===========================================================================
ACTOR CarPlayer : PlayerPawn
{
  player.forwardmove 0
  player.sidemove 0
  player.viewheight 32
  player.jumpz 0
  ReactionTime 0
  Health 500
  Radius 32
  Height 52
  Mass 100
  PainChance 0
  Player.ColorRange 112, 127
  Deathsound "weapons/rocklx"
  Player.CrouchSprite ""
  Player.DisplayName "Car"
  Player.SpawnClass "Car"
  Player.SoundClass "Car"
  Player.MorphWeapon "CarGo"
  ActiveSound "DSCARGO"
  +NOPAIN
  +NOBLOOD
  -PICKUP
  +PUSHABLE
  +NOSKIN
  States
  {
  Pain:
  Melee:
  Missile:
		GCAR A 0
		Goto See
  See:
  Spawn:
		GCAR A 0 A_JumpIfHealthLower(100,"Smoke")
		GCAR A 5 bright
		Loop
  Smoke:
   	goto Spawn+1
  AltSkinDeath:
  AltSkinXDeath:
  Death:
  XDeath:
    TNT1 A 0 A_Scream
    TNT1 A 0 A_StopSound (6)
    TNT1 A 0 A_StopSound (7)
    TNT1 A 0 A_Fall
  Remove:
    TNT1 A 10 A_CheckPlayerDone
    Wait
  }
}

ACTOR PowerCarPlayer : PowerMorph
{
 PowerMorph.PlayerClass "CarPlayer"
 PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_WHENINVULNERABLE)
 //PowerMorph.MorphStyle (MRF_ADDSTAMINA|MRF_FULLHEALTH|MRF_WHENINVULNERABLE)
 PowerMorph.MorphFlash "NoFlash"
 PowerMorph.UnMorphFlash "NoFlash"
}

ACTOR Car : PowerupGiver
{
 Powerup.Type PowerCarPlayer
 Powerup.Duration 0x7FFFFFFF
 Inventory.MaxAmount 0
 +INVENTORY.AUTOACTIVATE
 Radius 32
 Height 52
 Inventory.PickupSound "Car/Start"
 Inventory.PickupMessage "You got a Car!"
 States
 {
  Spawn:
   	GCAR A -1
   	Stop
 }
}

ACTOR CarPuff : BulletPuff
{
  +PAINLESS
  +FORCEXYBILLBOARD
  DamageType Car
  SeeSound "DSAUTOCR"
}

actor CarGo : Weapon
{
  Weapon.SelectionOrder 2000
  +WEAPON.DONTBOB
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  DamageType Car
  States
  {
  Ready:
    TNT1 A 0 A_StopSound
    TNT1 A 0 A_PlaySound ("Car/Idle", 7, 1.0)
    TNT1 A 0 ACS_ExecuteAlways(997,0)
    CARH A 1 A_WeaponReady
    goto Ready+2
  Fire:
    TNT1 A 0 A_LoopActiveSound
  Hold:
    TNT1 A 0 ACS_ExecuteAlways(997,0)
    TNT1 A 0 A_StopSound (7)
    TNT1 A 0 A_Recoil (-3)
    CARH A 1 A_CustomPunch (3,0,0,"CarPuff")
    TNT1 A 0 A_ReFire
    goto Ready
  Deselect:
    TNT1 A 0 A_StopSound (7)
    TNT1 A 0 A_StopSound
    TNT1 AAAAAAAAAAAAAAAA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Select:
    TNT1 A 0 A_StopSound (7)
    TNT1 A 0 A_StopSound
    TNT1 AAAAAAAAAAAAAAAA 0 A_Raise
    TNT1 A 1 A_Raise
    Loop
  AltFire:
		TNT1 A 10 A_PlaySound ("CARENTR", 7)
		TNT1 A 10 A_PlaySound ("CAREXIT", 7)
		TNT1 A 10 ACS_ExecuteAlways(998,0)
    Loop
  }
}

//===========================================================================

ACTOR MotorcyclePlayer : CarPlayer
{
  Player.DisplayName "Motorcycle"
  Player.SpawnClass "Motorcycle"
  Player.SoundClass "Motorcycle"
  Player.MorphWeapon "MotorcycleGo"
  player.viewheight 35
  Radius 16
  ActiveSound "BIKEGO"
  States
  {
  Pain:
  Melee:
  Missile:
   	BIKE A 0
   	Goto See
  See:
  Spawn:
   	BIKE A 0 A_JumpIfHealthLower(100,"Smoke")
   	BIKE A 5 bright
   	Loop
  Smoke:
	 	goto Spawn+1
  Death:
  XDeath:
    TNT1 A 0 bright A_Playsound("weapons/rocklx")
    TNT1 A 0 A_Scream
    TNT1 A 0 A_StopSoundEx (6)
    TNT1 A 0 A_StopSoundEx (7)
    TNT1 A 0 A_Fall
  Remove:
    TNT1 A 10 A_CheckPlayerDone
    Wait
  }
}

ACTOR PowerMotorcyclePlayer : PowerCarPlayer
{
 PowerMorph.PlayerClass "MotorcyclePlayer"
}


ACTOR Motorcycle : Car
{
 Inventory.PickupSound "BIKESTRT"
 Inventory.PickupMessage "You got the Motorcycle!"
 Powerup.Type PowerMotorcyclePlayer
 States
 {
  Spawn:
   BIKB A -1
   Stop
 }
}

actor MotorcycleGo : CarGo
{
  States
  {
  Ready:
    TNT1 A 0 A_StopSound
    TNT1 A 0 A_PlaySound ("BIKEIDLE", 7, 1, true)
    TNT1 A 0 ACS_ExecuteAlways(997,0)
    BIKH A 1 A_WeaponReady
    goto Ready+2
  Fire:
    TNT1 A 0 A_LoopActiveSound
  Hold:
    TNT1 A 0 ACS_ExecuteAlways(997,0)
    TNT1 A 0 A_StopSound (7)
    TNT1 A 0 A_CustomPunch (3,0,0,"CarPuff")
    BIKH A 0 A_Jump (224, "Off1", "Off2", "Off3", "Off4", "Off5", "Off6", "Off7", "Off8")
  Off1:
    BIKH A 1 OFFSET (1, 33) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off2:
    BIKH A 1 OFFSET (-2, 35) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off3:
    BIKH A 1 OFFSET (-3, 35) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off4:
    BIKH A 1 OFFSET (2, 34) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off5:
    BIKH A 1 OFFSET (3, 34) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off6:
    BIKH A 1 OFFSET (1, 35) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off7:
    BIKH A 1 OFFSET (2, 33) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  Off8:
    BIKH A 1 OFFSET (-3, 34) A_Recoil (-2)
    BIKH A 0 A_ReFire
    goto Ready
  AltFire:
    TNT1 A 10 A_PlaySound ("BIKEIDLE", 7)
    TNT1 A 10 ACS_ExecuteAlways(998,0)
    Loop
  }
}