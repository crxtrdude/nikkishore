ACTOR Girl1 : SwitchingDecoration 18100
{
	height 58
	radius 24
	scale 0.5
	Activation THINGSPEC_Switch|THINGSPEC_ThingTargets
	+USESPECIAL
	+SOLID
	States
	{
	Spawn:
		GRLA A 10
		Loop
	Active:
		// Prevent multiple dialogue activations
		TNT1 A 0 A_ChangeFlag("USESPECIAL", false)
		
		//
		// Make sure that you use ACS_NamedExecuteWithResult or CallACS for all scripts or
		// you will run into issues.
		//
		// Syntax:
		// SetHoldTime, <Hold Time>
		// AddDialogue, <Character ID>, <Graphic ID>, <Dialogue ID>
		// AddChoice, <Language Choice ID>, <Custom Choice ID>
		// AddChoiceStart, <Type>, <Numbered> // Type 0 = Default, Type 1 = Alternate, Type 2 = Default with UI, Type 3 = Alternate with UI
		// AddChoiceEnd // This script is a marker, so it takes no arguments
		// ChoiceMade // This script will return the custom choice ID of the last choice the player made
		// ChoiceDone // This script should be used inside A_JumpIf and followed by a "Wait"
		// IsQueueEmpty // This script should be used inside A_JumpIf and followed by a "Wait"
		// ForceFadeIn // Forces the next piece of dialogue to fade in
		// SetGraphicSize, <Width>, <Height>
		// SetGraphicPos, <X Pos> * 65536, <Y Pos> * 65536
		// VoiceActingOn // Turns on voice acting
		// VoiceActingOff // Turns off voice acting
		// SetFadeTime, <Fade Time> * 65536
		//
		// Commands that take floating point values (such as SetGraphicPos and SetFadeTime)
		// must have " * 65536" after the values. This is due to a decorate limitation.
		//
		// The "custom choice ID" is a number that you associate with the choice,
		// which is unique to the choice. "ChoiceMade" uses this number rather
		// than the language choice ID because this way it allows more control over conversations.
		//
		// If the Graphic ID is -1, then no graphic will be displayed.
		// This is useful if you want to have dialogue with no portrait.
		//
		TNT1 A 0 A_FaceTarget
		
		TNT1 A 0 ACS_NamedExecuteWithResult("SetHoldTime", -1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_CLUBGIRL1, -1, 1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoiceStart") // Using AddChoiceStart with no parameters will make it use type 0 without numbering
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoice", 20, 1)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoice", 50, 2)
		TNT1 A 0 ACS_NamedExecuteWithResult("AddChoiceEnd")
		GRLA A 1 A_JumpIf(CallACS("ChoiceDone"), 1)
		Wait
		TNT1 A 0 A_JumpIf(1 == CallACS("ChoiceMade"), "Choice1")
		TNT1 A 0 A_JumpIf(2 == CallACS("ChoiceMade"), "Choice2")
		Goto Done
		
	Choice1:
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_CLUBGIRL1, -1, 2)
		Goto Done
	Choice2:
		TNT1 A 0 ACS_NamedExecuteWithResult("AddDialogue", VNC_CLUBGIRL1, -1, 3)
		Goto Done
		
	Done:
		// Here we wait for the stack to empty (all dialogue to be cleared)
		GRLA A 1 A_JumpIf(CallACS("IsQueueEmpty"), 1)
		Wait
		TNT1 A 0 A_ChangeFlag("USESPECIAL", true)
		Goto Spawn
	}
}