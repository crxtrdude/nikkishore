ACTOR ItemStunner : CustomInventory 15000
{
  //$Category Nikki Weapons
  //$Sprite TAZIA0
  //$Title "Stunner"
	Radius 16
	Height 8
	Scale 0.5
	Inventory.PickupMessage "You got the stun gun."
	States
	{
	Spawn:
		TAZI A 1
		Loop
	Pickup: 
		TAZI A 0 
		TAZI A 0 A_PlaySound("world/equip")
		TAZI A 0 A_GiveInventory("Stunner",1)
		Stop
	}
}

ACTOR Stunner : Weapon
{
	Weapon.SelectionOrder 450
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 50
	Weapon.AmmoUse 2	
	+Weapon.NoAlert
	+NoAutoFire
	Weapon.SlotNumber 1
	States
	{
		Ready:
			TAZR A 2
			TAZR B 3
			TAZR C 3
			TAZR D 4
			Goto Bobbing
		Bobbing: //The 'real' ready state
			TAZR D 2 A_WeaponReady
			Loop
		Select:
			TAZR A 0 A_Raise
			TAZR A 1 A_Raise
			Loop
		Deselect:
			TAZR D 5
			TAZR C 3
			TAZR B 3
			TAZR A 2
			Goto Returned
		Returned:
			TNT1 A 1 A_Lower
			Loop
		Fire: //Stun
			TAZR D 4 Offset(29,19)
			TAZR D 3 Offset(73,43)			 
			TAZR E 1 Offset(37,18) A_WeaponReady
			TAZR E 1 Offset(4,9) A_PlaySound("weapons/nikkiattack")
			TAZR F 18 Offset(-13,0) A_Saw("weapons/stunnerhit","weapons/stunnerhit", 4, "StunnerDummy")
			TAZR E 2 Offset(14,24)
			TAZR E 2 Offset(86,51) 
			TAZR E 0 A_Refire 
			Goto Bobbing
		AltFire: //Melee
			TAZR D 3 Offset(43, 25)
			TAZR G 3 Offset(73, 58)
			TAZR G 2 Offset(108, 85)
			TAZR H 2 Offset(63, -36)
			TAZR H 2 Offset(3, -16)
			TAZR I 1 Offset(-12, 10) A_PlaySound("world/swing")
			TAZR J 1 Offset(37, 22) A_CustomPunch(3)
			TAZR J 1 Offset(23, 59)
			TAZR J 2 Offset(7, 96)
			TAZR J 2 Offset(1, 124)
			TAZR D 2 Offset(9, 118)
			TAZR D 3 Offset(12, 64)
			TNT1 A 1 Offset(12, 64)
			Goto Bobbing
		
	}
}