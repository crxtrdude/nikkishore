ACTOR NikkiFist: Fist Replaces Fist
{
	+CHEATNOTWEAPON
	+NOAUTOFIRE
	States
	{
		Spawn:
			TNT1 A -1
			Loop
		Ready:
			NFST A 1 A_WeaponReady
			Goto Ready
		Deselect:
			NFST A 0 A_Lower
			NFST A 1 A_Lower
			Loop
		Select:
			NFST A 0 A_Raise
			NFST A 1 A_Raise
			Loop
		Fire:
			NFST F 1 A_PlaySound("weapons/nikkiattack")
			NFST F 2 
			NFST G 1
			NFST H 1 A_PlaySound("world/swing",1)
			NFST H 5 A_CustomPunch(2, 0, 0, "MeleePuff")
			NFST F 4
			NFST A 3
			NFST A 0 A_ReFire
			Goto Ready
		AltFire:
			NFST A 0 A_JumpIfInventory("DoubleTapCheck",1,"Uppercut")
			NFST A 0 A_GiveInventory("DoubleTapCheck",1)
			NFST I 8 A_WeaponReady
			NFST A 0 A_TakeInventory("DoubleTapCheck",999)
			NFST I 0 
			NFST J 2 A_PlaySound("weapons/nikkiattack2")
			NFST J 1
			NFST J 1 A_PlaySound("world/swing",1) 
			NFST K 6 A_CustomPunch(4, 0, 0, "BulletPuff")
			NFST I 4
			NFST I 0 A_ReFire
			Goto Ready
		Uppercut:
			NFST F 2 A_FireCustomMissile ("SledgeHammerDummy", 0, 0)
			NFST L 2 SetPlayerProperty(0,1,0)
			NFST M 2 A_PlaySound("weapons/nfistuppercut") 
			NFST M 0 SetPlayerProperty(0,0,0)
			NFST M 0 SetPlayerProperty(0,1,4)
			NFST M 0 
			NFST N 1 A_PlaySound("world/swing",1)
			NFST O 1 
			NFST P 1 A_CustomPunch(random(35,40), 1, 0, "BulletPuff", 58)
			NFST P 10
			NFST O 5 SetPlayerProperty(0,0,4)
			NFST A 0 A_TakeInventory("DoubleTapCheck",999)
			Goto Ready
	}
}

ACTOR DoubleTapCheck : Ammo
{
	inventory.maxamount 1
	ammo.backpackamount 0
	ammo.backpackmaxamount 0
	inventory.icon TNT1A0
}