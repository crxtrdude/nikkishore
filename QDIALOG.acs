//==========================================================
// Nikki Shore
// QDialog - Immediary for QVN
//
// Code by CRxTRDude.
//==========================================================

#library "qdialog"
#include "zcommon.acs"
#import "qvn.acs"

str dialnext;
int fromchoice;
int radioinit;
int radiofirst;
str radiosecond;

//DoDialog:
//Checks if the language.vn string contains the following:
//<COMMAND> <ATTRIBUTES>
//Valid commands are DIAL, CHOI and PORT
//
//DIAL <TEXT> <NEXT>
//CHOI <CHOICES STRING>
//PORT <PORTRAIT STRING> <NEXT>
//CMND <NAMED SCRIPT> <NEXT>
//
//For choices, do not mention a next number
//since you could use DIAL to lead to the next
//string sets.
//

Script "DoDialog" (int Text){
	log(s:"\n----\nDoDialog");
	Delay(1);	
	str findialog;
	str pretext;
	str com, first;
	int initlen;
	int strl;
	
	If (VN_DisplayState == 1){
		findialog = StrParam(l:Text);
		initlen = StrLen(Text)-4;
		pretext = StrLeft(Text, initlen);
		com = StrMid(findialog,0,4);
		log(s:"> com = ",s:com);
		
		if (com == "DIAL"){
			strl = StrLen(findialog)-10;
			first = StrMid(findialog,5,strl);
			if (radioinit){
				Delay(1);
				ACS_NamedExecuteWait("VN_Parser",0,first,True);
				//Draw the radio portrait first if radio is used.
				ACS_NamedExecute("VN_DrawPortrait",0,radiofirst);
			} 
			Delay(3);
			ACS_NamedExecuteWait("VN_DrawText",0,first);
		}else if (com == "PORT"){
			strl = StrLen(findialog)-10;
			first = StrMid(findialog,5,strl);
			Delay(3);
			ACS_NamedExecuteWait("VN_DrawPortrait",0,first);
			if (VN_currentState){
				radioinit = true;
				radiofirst = first;		
			}
		}else if (com == "CHOI"){
			fromchoice = 0;
			strl = StrLen(findialog)-5;
			first = StrMid(findialog,5,strl);
			dialnext = Text;
			Delay(3);
			ACS_NamedExecuteWithResult("VN_DrawChoices",first);
		}else if (com == "CMND"){
			strl = StrLen(findialog)-10;
			first = StrMid(findialog,5,strl);
			log(s:"> command = ",s:first);
			ACS_NamedExecute(first,0);
		}else{
			log(s:"QDIALOG - Corrupted.");
			terminate;
		}
		
		if (com != "CHOI"){
			dialnext = StrParam(s:pretext,s:StrRight(findialog,4));
		}
		
		log(s:"> findialog = ",s:findialog);
		log(s:"> first = ",s:first);
		log(s:"> dialnext = ",s:dialnext);
		Delay(1);
		
		If(StrRight(findialog,4) == "0END" && com == "CMND"){
			log(s:"> Ending ...");
			ACS_NamedExecute("VN_Stop",0);
			VN_currentChoice = 0;
			SetResultValue (2);
			terminate;
		}
		
		if (com == "PORT" || com == "CMND") ACS_NamedExecute("DoDialogCatch",0);
		Delay(1);
		
	}Else{
		Log(s:"QDIALOG - QVN is not initialized! Check script if VN_Show function is called!");
		Terminate;
	}
	
	ACS_NamedExecuteWait("VN_Update",0);
	Delay(1);
	
	If(VN_currentState == 2){
		Log(s:"> VN_choiceState = ",i:VN_choiceState);
		if(VN_choiceState == 3){
			fromchoice = 1;
			Log(s:">> Break - Pressed enter");
		}else{
			Log(s:">> Break - Moving pointer");
		}
	}Else{
		Log(s:">> Break");
	}
	
	If(StrRight(findialog,4) == "0END" && com != "CMND"){
		log(s:"> Ending ...");
		ACS_NamedExecute("VN_Stop",0);
		VN_currentChoice = 0;
		SetResultValue (2);
		terminate;
	}
	
	ACS_NamedExecute("DoDialogCatch",0);
}

Script "DoDialogCatch" (void){
	ACS_NamedTerminate("DoDialog",0);
	ACS_NamedTerminate("VN_KeyInput",0);
	Log(s:"DoDialogCatch");
    Delay(1);
	log (s:"> VN_currentState = ",i:VN_currentState);
	log (s:"> vnPrevState = ",i:vnPrevState);
    if (fromchoice == 1){
		VN_currentState = vnPrevState;
		vnPrevState = 0;
		fromchoice = 0;
		VN_choiceState = 0;
		VN_Show(VN_currentState);
		Delay(1);
		ACS_NamedExecute("DoDialog",0,VN_choiceNext);			
    }else{
		Log(s:"> dialnext = ",s:dialnext);
    	ACS_NamedExecute("DoDialog",0,dialnext);
	}
}

Function void QD_Init (int type){
    dialnext = "";
	radioinit = false;
		
    VN_Show(type);
}